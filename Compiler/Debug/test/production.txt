Program -> ProgramHead DeclarePart ProgramBody
ProgramHead -> PROGRAM ProgramName
ProgramName -> ID
DeclarePart -> TypeDec VarDec ProcDec
TypeDec -> epsilon
TypeDec -> TypeDeclaration
TypeDeclaration -> TYPE TypeDecList
TypeDecList -> TypeId = TypeName ; TypeDecMore
TypeDecMore -> epsilon
TypeDecMore -> TypeDecList
TypeId -> ID
TypeName -> BaseType
TypeName -> StructureType
TypeName -> ID
BaseType -> INTEGER
BaseType -> CHAR
StructureType -> ArrayType
StructureType -> RecType
ArrayType -> ARRAY [ Low .. Top ] OF BaseType
Low -> INTC
Top -> INTC
RecType -> RECORD FieldDecList END
FieldDecList -> BaseType IdList ; FieldDecMore
FieldDecList -> ArrayType IdList ; FieldDecMore
FieldDecMore -> epsilon
FieldDecMore -> FieldDecList
IdList -> ID IdMore
IdMore -> epsilon
IdMore -> , IdList
VarDec -> epsilon
VarDec -> VarDeclaration
VarDeclaration -> VAR VarDecList
VarDecList -> TypeName VarIdList ; VarDecMore
VarDecMore -> epsilon
VarDecMore -> VarDecList
VarIdList -> ID VarIdMore
VarIdMore -> epsilon
VarIdMore -> , VarIdList
ProcDec -> epsilon
ProcDec -> ProcDeclaration
ProcDeclaration -> PROCEDURE ProcName ( ParamList ) ; ProcDecPart ProcBody ProcDecMore
ProcDecMore -> epsilon
ProcDecMore -> ProcDeclaration
ProcName -> ID
ParamList -> epsilon
ParamList -> ParamDecList
ParamDecList -> Param ParamMore
ParamMore -> epsilon
ParamMore -> ; ParamDecList
Param -> TypeName FormList
Param -> VAR TypeName FormList
FormList -> ID FidMore
FidMore -> epsilon
FidMore -> , FormList
ProcDecPart -> DeclarePart
ProcBody -> ProgramBody
ProgramBody -> BEGIN StmList END
StmList -> Stm StmMore
StmMore -> epsilon
StmMore -> ; StmList
Stm -> ConditionalStm
Stm -> LoopStm
Stm -> InputStm
Stm -> OutputStm
Stm -> ReturnStm
Stm -> ID AssCall
AssCall -> AssignmentRest
AssCall -> CallStmRest
AssignmentRest -> VariMore := Exp
ConditionalStm -> IF RelExp THEN StmList ELSE StmList FI
LoopStm -> WHILE RelExp DO StmList ENDWH
InputStm -> READ ( Invar )
Invar -> ID
OutputStm -> WRITE ( Exp )
ReturnStm -> RETURN ( Exp )
CallStmRest -> ( ActParamList )
ActParamList -> epsilon
ActParamList -> Exp ActParamMore
ActParamMore -> epsilon
ActParamMore -> , ActParamList
RelExp -> Exp OtherRelE
OtherRelE -> CmpOp Exp
Exp -> Term OtherTerm
OtherTerm -> epsilon
OtherTerm -> AddOp Exp
Term -> Factor OtherFactor
OtherFactor -> epsilon
OtherFactor -> MultOp Term
Factor -> ( Exp )
Factor -> INTC
Factor -> Variable
Variable -> ID VariMore
VariMore -> epsilon
VariMore -> [ Exp ]
VariMore -> . FieldVar
FieldVar -> ID FieldVarMore
FieldVarMore -> epsilon
FieldVarMore -> [ Exp ]
CmpOp -> <
CmpOp -> =
AddOp -> +
AddOp -> -
MultOp -> *
MultOp -> /